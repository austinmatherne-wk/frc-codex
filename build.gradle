plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.frc.codex'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:3.3.4"
	developmentOnly 'org.springframework.boot:spring-boot-devtools:3.3.4'
	implementation 'com.google.guava:guava:33.3.1-jre'
	implementation 'com.zaxxer:HikariCP:6.0.0'
	implementation 'org.flywaydb:flyway-core:10.20.0'
	implementation 'org.flywaydb:flyway-database-postgresql:10.20.0'
	implementation 'org.postgresql:postgresql:42.7.4'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:3.3.4'
	implementation 'org.springframework.boot:spring-boot-starter-web:3.3.4'
	implementation 'software.amazon.awssdk:lambda:2.28.24'
	implementation 'software.amazon.awssdk:netty-nio-client:2.28.24'
	implementation 'software.amazon.awssdk:s3:2.28.24'
	implementation 'software.amazon.awssdk:sqs:2.28.24'
	testImplementation 'org.mockito:mockito-junit-jupiter:5.14.2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:3.3.4'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.11.2'
}

tasks.named('test') {
	useJUnitPlatform()
	environment "AWS_REGION", "eu-west-2"
}

tasks.register('npmInstall', Exec) {
	description = 'Run npm install.'
	commandLine 'npm', 'install'
}

tasks.register('copyNodeModules', Copy) {
	dependsOn 'npmInstall'
	description = 'Copy node_modules assets.'
	into "src/main/resources/static"
	into("/") {
		from "node_modules/govuk-frontend/dist/govuk/govuk-frontend.min.css"
		from "node_modules/govuk-frontend/dist/govuk/govuk-frontend.min.js"
	}
	into("assets") {
		from "node_modules/govuk-frontend/dist/govuk/assets/manifest.json"
	}
	into("assets/fonts") {
		from "node_modules/govuk-frontend/dist/govuk/assets/fonts"
	}
	into("assets/images") {
		from "node_modules/govuk-frontend/dist/govuk/assets/images"
	}

}

tasks.named('processResources') {
	dependsOn 'copyNodeModules'
}
